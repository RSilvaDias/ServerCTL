uses data.adt.List

const char debugMSG[] = "[@RemoteDist]"
const int DEFAULT_DIST_PORT = 8081
const int DEFAULT_APP_PORT  = 2010

component provides App requires io.Output out, net.TCPSocket, net.TCPServerSocket,
	data.StringUtil strUtil, composition.Assembly assembly, data.json.JSONEncoder encoder,
	data.IntUtil iu {

	IDCReference remote[] 	= null
	int portDist 			= 0
	int portApp				= 0
	bool running 			= false

	void startRemoteProcess(char remoteP[],char nameP[]) {
		// load and start relocated component
		if (!running) {
			AppParam params[] = new AppParam[1]
			params[0] = new AppParam()
			params[0].string = iu.intToString(portApp)
			assembly.setMain(remoteP, params)
			assembly.removeComp(new String[](
				new String("./mr/getMRProxy.o"),
				new String("./mrr/getMRRProxy.o"),
				new String("./hd/getHDProxy.o"),
				new String("./hdr/getHDRProxy.o")))
			out.println("$(debugMSG) Remote Component $(remoteP) Started! ")
			running = true
			if (nameP == "mr"){
				remote = assembly.getCompsByProvidedInterface("proxy.DistProxy")
			}	else if (nameP == "mrr"){
				remote = assembly.getCompsByProvidedInterface("proxy.DistProxyMRR")
			}	else if (nameP == "hd"){
				remote = assembly.getCompsByProvidedInterface("proxy.DistProxyHD")
			}	else if (nameP == "hdr"){
				remote = assembly.getCompsByProvidedInterface("proxy.DistProxyHDR")
			}
		}
		if ((remote == null) || (remote.arrayLength > 1)) { throw new Exception("Something went wrong!") }
		else {

		}
	}

	void handleRequest(TCPSocket client) {
		// reading data from socket -- this could be optimised
		char buf[] = null
		int len = 0
		char command[] = null
		while ((buf = client.recv(1)).arrayLength > 0) {
			command = new char[](command, buf)
			len++
			//stop condition
			if (len >= 4) {
				if ((command[len-4] == "\r") && (command[len-3] == "\r") &&
					(command[len-2] == "\r") && (command[len-1] == "\r")) {
					break
				}
			}
		}

		// processing command
		String helper[] = strUtil.explode(command, "!")
		out.println(" TESTING : $(helper[0].string)")
		if (helper[0].string == "../distributor/RemoteMR.o") {
			startRemoteProcess(helper[0].string,"mr")
		}else if (helper[0].string == "../distributor/RemoteMRR.o") {
			startRemoteProcess(helper[0].string,"mrr")
		}else if (helper[0].string == "../distributor/RemoteHD.o") {
			startRemoteProcess(helper[0].string,"hd")
		}else if (helper[0].string == "../distributor/RemoteHDR.o") {
			startRemoteProcess(helper[0].string,"hdr")	
		}
		client.send("OK")
		client.disconnect()
	}

	int App:main(AppParam params[]) {
		if (params.arrayLength == 2) {
			portDist = iu.intFromString(params[0].string)
			portApp = iu.intFromString(params[1].string)
		} else {
			portDist = DEFAULT_DIST_PORT
			portApp = DEFAULT_APP_PORT
		}

		// instantiating variables
		TCPServerSocket host = new TCPServerSocket()

		// binding server socket
		if (!host.bind(TCPServerSocket.ANY_ADDRESS, portDist)) {
			out.println("Error: failed to bind master socket")
			return 1
		}

		out.println("$(debugMSG) Remote Distributor started!")
		while (true) {
			TCPSocket client = new TCPSocket()
			if (client.accept(host))
				asynch::handleRequest(client)
		}

		return 0
	}
}
